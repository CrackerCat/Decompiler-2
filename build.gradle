plugins {
    id("application")
    id("com.github.johnrengelman.shadow") version("8.1.1")
    id("io.freefair.lombok") version("8.1.0")
    id("java")
}

group = "com.sotasan"
version = "0.8.6"

repositories {
    mavenCentral()
}

application {
    mainClass = "${project.group}.${project.name.toLowerCase()}.Main"
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

dependencies {
    implementation("com.fifesoft:rsyntaxtextarea:3.3.4")
    implementation("com.formdev:flatlaf:3.1.1")
    implementation("com.formdev:flatlaf-extras:3.1.1")
    implementation("com.miglayout:miglayout:3.7.4")
    implementation("io.github.classgraph:classgraph:4.8.161")
    implementation("org.benf:cfr:0.152")
    implementation("org.bitbucket.mstrobel:procyon-compilertools:0.6.0")
    implementation("org.jetbrains:annotations:24.0.1")
    implementation("org.vineflower:vineflower:1.9.2")
}

processResources {
    outputs.upToDateWhen { false }
    filesMatching("application.properties") {
        expand(project.properties)
    }
}

jar {
    enabled = false
}

shadowJar {
    dependsOn(distTar, distZip)
    archiveBaseName = project.name.toLowerCase()
    archiveClassifier = null
}

test {
    enabled = false
}

tasks.register("testJar", Jar) {
    archiveFileName = "test.jar"
    from(sourceSets.test.output)
}

run {
    dependsOn(testJar)
    args((testJar as Jar).archiveFile.get())
}