plugins {
    id("java")
    id("io.freefair.lombok") version "8.1.0"
}

group = "com.hohltier"
version = "0.8.2"

repositories {
    mavenCentral()
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

configurations {
    implementation {
        canBeResolved = true
    }
}

dependencies {
    implementation("com.fifesoft:rsyntaxtextarea:3.3.4")
    implementation("com.formdev:flatlaf:3.1.1")
    implementation("com.miglayout:miglayout:3.7.4")
    implementation("io.github.classgraph:classgraph:4.8.161")
    implementation("org.benf:cfr:0.152")
    implementation("org.bitbucket.mstrobel:procyon-compilertools:0.6.0")
    implementation("org.jetbrains:annotations:24.0.1")
    implementation("org.vineflower:vineflower:1.9.1")
    runtimeOnly("com.formdev:flatlaf-extras:3.1.1")
}

processResources {
    outputs.upToDateWhen { false }
    filesMatching("application.properties") {
        expand(project.properties)
    }
}

jar {
    archiveBaseName = project.name.toLowerCase()
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    manifest.attributes["Main-Class"] = "${project.group}.${project.name.toLowerCase()}.Main"
    from { configurations.implementation.collect { it.directory ? it : zipTree(it) } }
}

tasks.register("testJar", Jar) {
    archiveFileName.set("test.jar")
    manifest.attributes["Main-Class"] = "${project.group}.${project.name.toLowerCase()}.Main"
    from(sourceSets.test.output)
}